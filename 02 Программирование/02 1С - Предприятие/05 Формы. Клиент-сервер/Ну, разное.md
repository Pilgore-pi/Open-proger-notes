#### Последовательность событий при вводе по строке

![[Схема_событий_при_вводе_по_строке.png]]

Пользователь начинает вводить текст в поле.

Дальше возможны два варианта развития событий:

- В процессе ввода он делает паузу. В этом случае будет вызвано событие Автоподбор.
- Находясь в поле ввода, в которое уже введена часть наименования, он нажимает стрелку вниз. Например, после паузы платформа показала список выбора, но пользователь случайно закрыл его. В этом случае также будет вызвано событие Автоподбор.
- В процессе ввода он не делает пауз и, введя часть наименования, переходит к следующему элементу формы, нажав Tab или Enter. В этом случае будет вызвано событие Окончание ввода текста (рис. 3.190).



События Автоподбор и Окончание ввода текста – это клиентские события поля формы, вернее, его расширения – расширения поля ввода.

После обработки одного из этих событий платформа вызывает событие Обработка получения данных выбора. Это событие вызывается в модуле менеджера того объекта конфигурации, значение которого хранится в этом поле. В нашем случае это будет событие, обрабатываемое в модуле менеджера справочника Поставщики.

После обработки этого события платформа показывает рядом с полем ввода список выбора – список возможных значений, которые могут быть помещены в это поле.

Пользователь выбирает одно из значений, после чего возникает событие поля – Обработка выбора.

После обработки этого события платформа помещает выбранное значение в поле ввода и вызывает последнее событие поля – При изменении.

Чаще всего при работе с вводом по строке разработчик решает две задачи:

- сформировать список выбора по своим правилам;
- отказаться от выбора, сделанного пользователем, или подставить в поле собственное значение.

Вторая задача довольно простая и решается в обработчике Обработка выбора. Подробнее этот вопрос рассматривается в разделе [«Событие "Обработка выбора"»](https://its.1c.ru/db/pubv8devui/content/259/hdoc/h259).

А вот для решения первой задачи существует большое количество возможностей, которые мы сейчас и рассмотрим.

#### Формирование собственного списка выбора

Как видно из приведенной выше схемы, в любом случае при вводе по строке вызываются два события:

- Автоподбор или Окончание ввода текста;
- Обработка получения данных выбора.

Примечательным здесь является то, что первые события вызываются в форме, в то время как последнее событие вызывается в модуле менеджера прикладного объекта.

Из этого следует важное замечание: переопределять формирование списка выбора нужно прежде всего в обработчике Обработка получения данных выбора. Потому что это будет работать во всех случаях, когда в каком-либо поле, в какой-либо форме будет формироваться список выбора значений этого типа. Даже в тех формах, которые генерируются платформой автоматически и которые разработчик изменить не может.

Более сложный случай – когда в одной или нескольких формах нужно иметь особенное формирование списка выбора, не такое, как в остальных местах. В этом случае нужно использовать обработчики событий этой (этих) формы: Автоподбор и Окончание ввода текста. В простейшем варианте в этих обработчиках должен быть написан одинаковый алгоритм.

Самым сложным и специфическим является случай, когда в некоторой форме список выбора при паузе и при переходе к следующему элементу должен формироваться по разным алгоритмам. Тогда в каждом из обработчиков Автоподбор и Окончание ввода текста будет собственный алгоритм формирования списка выбора.

##### Событие «Обработка получения данных выбора»

https://its.1c.ru/db/pubv8devui#content:257:hdoc

Итак, рассмотрим возможности, которые существуют в обработчике события Обработка получения данных выбора.

Синтаксис описания этого обработчика выглядит следующим образом (листинг 3.150).

Листинг 3.150. Объявление обработчика события «Обработка получения данных выбора»

```bsl
ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
```

ДанныеВыбора – это переменная, в которую разработчик должен поместить собственный список выбора. При входе в обработчик этот параметр не содержит никаких значений, то есть через него нельзя получить доступ к списку выбора, который сформирует платформа. Просто потому, что этот список формируется уже после выхода из этого обработчика. Однако может возникнуть желание в этом обработчике получить список, формируемый платформой, и добавить в него (удалить) несколько элементов. Как это сделать – рассказано в разделе [«Метод "ПолучитьДанныеВыбора()"»](https://its.1c.ru/db/pubv8devui/content/258/hdoc/h258).

Переменная Параметры содержит набор параметров, которые платформа будет использовать для формирования списка выбора. Особенность заключается в том, что при формировании списка выбора платформа будет учитывать свойства Параметры выбора и Связи параметров выбора, заданные для соответствующего реквизита объекта конфигурации. Поэтому в параметрах могут содержаться какие-то отборы.

СтандартнаяОбработка – это булева переменная, на основе которой платформа определяет, что делать после выхода из этого обработчика.

Если СтандартнаяОбработка равна Истина, то платформа самостоятельно сформирует список выбора исходя из того, что указано в Параметрах.

Если СтандартнаяОбработка равна Ложь, платформа не будет формировать список выбора самостоятельно, а покажет то, что находится в параметре ДанныеВыбора.

Таким образом, у нас есть следующие возможности:

- изменить Параметры и сказать платформе, чтобы она сформировала список выбора;
- отказаться от стандартной обработки и самостоятельно сформировать список выбора.

Сначала рассмотрим, каким образом можно модифицировать параметры.

Примечание

Пример можно посмотреть в демонстрационной базе «Поле ввода», документ Накладная, реквизит Товар табличной части.

Допустим, есть документ Накладная, в табличной части которого необходимо подбирать товары. Причем для выбора должны предлагаться не все возможные товары, а только те, которые поставляет поставщик, указанный в этом документе, и те, которые не помечены на удаление.

Для этого у реквизита табличной части Товар заданы свойства Связи параметров выбора и Параметры выбора (рис. 3.191).

![[Свойства_конфигурация_01.png]]

В связях параметров выбора указывается, что для выбора будут предлагаться только товары поставщика, указанного в документе. В параметрах выбора указывается, что кроме этого для выбора будут предлагаться только товары, не помеченные на удаление.

В результате при наборе наименования товара в форме будет предложен следующий список товаров (рис. 3.192).

![[Свойства_конфигурация_02.png]]

Если мы посмотрим на все имеющиеся товары, то заметим, что подбор товаров произведен правильно (рис. 3.193).

![[Свойства_конфигурация_03.png]]

Допустим, нас не устраивает, что для выбора предлагаются те элементы справочника, которые являются услугами.

Мы можем легко исправить это сразу двумя способами. Можно просто, ничего не программируя, добавить этот отбор в свойство Параметры выбора реквизита табличной части Товар.

Но, если нужно, можно сделать это программно в обработчике события Обработка получения данных выбора в модуле менеджера справочника Товары.

Благодаря свойствам Параметры выбора и Связи параметров выбора в этом обработчике структура параметров содержит следующие отборы (рис. 3.194).

![[Свойства_конфигурация_04.png]]

Добавим к ним еще одно условие – что товар не должен быть услугой, а стандартную обработку оставим в значении Истина, чтобы платформа сформировала список выбора самостоятельно

```bsl
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Параметры.Отбор.Вставить("ВидТовара", Перечисления.ВидыТоваров.Товар);
КонецПроцедуры
```

Если теперь выполнить те же самые операции, мы увидим, что для выбора предлагаются только элементы, являющиеся товарами

![[Свойства_конфигурация_05.png]]

Рассмотрим вторую ситуацию, когда мы полностью самостоятельно формируем список выбора, не используя стандартные возможности платформы.

Здесь есть два случая. Список выбора может быть «простым» и «сложным».

- В простом случае это должен быть список значений, содержащий выбираемые значения (ссылки).
- В сложном случае это будет список значений, содержащий в качестве своих элементов структуры. Каждая такая структура содержит само выбираемое значение и некоторую дополнительную информацию об этом значении.

Сначала посмотрим, как можно сформировать простой список.

Допустим, в документе Накладная есть реквизит Склад. Тогда в модуле менеджера справочника Склады мы можем написать следующий обработчик

```bsl
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|        Склады.Ссылка
	|ИЗ
	|        Справочник.Склады КАК Склады
	|ГДЕ
	|        Склады.Розничный = ЛОЖЬ
	|        И Склады.Наименование ПОДОБНО &СтрокаПоиска";
	
	Запрос.УстановитьПараметр("СтрокаПоиска", "%" + Параметры.СтрокаПоиска + "%");
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Список = Новый СписокЗначений;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Список.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	ДанныеВыбора = Список;
КонецПроцедуры
```

В нем мы прежде всего отказываемся от стандартной обработки.

Затем формируем запрос, который выберет нам все склады, не являющиеся розничными и наименование которых содержит строку, введенную пользователем в поле ввода.

Строку, введенную пользователем, мы получаем из структуры параметров, в ней она содержится отдельным элементом с ключом СтрокаПоиска

```bsl
Запрос.УстановитьПараметр("СтрокаПоиска", "%" + Параметры.СтрокаПоиска + "%");
```

атем создаем пустой список значений и, обходя результат запроса, заполняем список ссылками. В конце помещаем этот список в переменную ДанныеВыбора.

В результате при вводе наименования склада мы будем иметь следующую ситуацию (рис. 3.196).

![[Свойства_конфигурация_06.png]]

Если посмотреть на список складов, мы увидим, что выбор произведен правильно, а фрагмент наименования склада может быть произвольным, не обязательно находящимся в начале наименования

![[Свойства_конфигурация_07.png]]

Допустим, в документе Накладная есть реквизит Поставщик. Тогда в модуле менеджера справочника Поставщики мы можем написать следующий обработчик

```bsl
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|        Поставщики.Ссылка,
	|        Поставщики.ПометкаУдаления,
	|        Поставщики.Ненадежный
	|ИЗ
	|        Справочник.Поставщики КАК Поставщики
	|ГДЕ
	|        Поставщики.Наименование ПОДОБНО &СтрокаПоиска";
	
	Запрос.УстановитьПараметр("СтрокаПоиска", "%" + Параметры.СтрокаПоиска + "%");
	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Список = Новый СписокЗначений;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Структура = Новый Структура;
		Структура.Вставить("Значение", ВыборкаДетальныеЗаписи.Ссылка);
		Структура.Вставить("ПометкаУдаления", ВыборкаДетальныеЗаписи.ПометкаУдаления);
		Если ВыборкаДетальныеЗаписи.Ненадежный Тогда
			Структура.Вставить(
				"Предупреждение",
				"Это ненадежный поставщик, его лучше не выбирать!"
			);
		КонецЕсли;
		Список.Добавить(Структура);
	КонецЦикла;
	
	ДанныеВыбора = Список;
КонецПроцедуры
```

Здесь, как и раньше, отказываемся от стандартной обработки и запросом получаем нужные ссылки.

Затем создаем пустой список значений, а при каждом обходе результата запроса создаем структуру, в которую помещаем:

- Само значение – в элемент с ключом Значение.
- Признак пометки удаления – в элемент с ключом ПометкаУдаления. Если пользователь выберет поставщика, помеченного на удаление, платформа выдаст стандартное предупреждение.
- Для тех поставщиков, которые являются ненадежными, мы добавляем в структуру элемент с ключом Предупреждение. Если пользователь выберет ненадежного поставщика, ему будет выдано добавленное нами предупреждение.

После заполнения структуры добавляем ее в список значений, а после обхода всего результата запроса список помещаем в переменную ДанныеВыбора.

В результате при вводе наименования поставщика список выбора будет выглядеть следующим образом

![[Свойства_конфигурация_08.png]]

На примере имеющихся поставщиков мы сможем посмотреть все возможные варианты работы «сложного» списка выбора

![[Свойства_конфигурация_09.png]]

Если выбрать Поставщик 1, мы не получим никаких предупреждений.

Если выбрать поставщика, помеченного на удаление (Поставщик11), мы получим стандартное предупреждение платформы

![[Свойства_конфигурация_10.png]]

Если выбрать ненадежного поставщика, да еще и помеченного на удаление (Поставщик367), мы получим «свое» предупреждение

![[Свойства_конфигурация_11.png]]

Таким образом, попутно мы убедились в следующем: если для выбираемого элемента указаны и пометка удаления, и предупреждение, будет выведено предупреждение.

Несколько слов о производительности. Событие Обработка получения данных выбора, обрабатываемое в модуле менеджера объекта, вызывается довольно часто. Перечислим эти случаи еще раз:

- Во-первых, при автоподборе, то есть когда возникает пауза при наборе текста в поле ввода.
- Во-вторых, при вводе по строке, то есть когда в поле ввода уже введена какая-то строка и фокус ввода переходит на другой элемент формы.
- И в-третьих (о чем не упоминалось в этом разделе), при быстром выборе, когда для выбора объекта используется не его основная форма выбора, а небольшой список, формируемый системой.

Поэтому при написании обработчика Обработка получения данных выбора нужно особенно тщательно подходить к вопросу эффективности создаваемого кода.

https://its.1c.ru/db/pubv8devui#content:258:hdoc Еще одна статья