[Источник](https://xn----1-bedvffifm4g.xn--p1ai/news/async-metods-article/)

При разработке конфигураций следует избегать длительных вызовов из клиентского кода в серверный. Все длительные серверные вызовы, которые могут выполняться более 8 секунд в обычных сценариях работы пользователя, следует выполнять асинхронно, с помощью фонового задания.

К таким операциям относятся: формирование отчета, групповая обработка объектов, загрузка или выгрузка данных в другое приложение, заполнение больших табличных частей и т.п.

В противном случае такие вызовы могут привести к потере работоспособности приложения или затруднению работы с ним:

браузер может предложить прекратить длительно выполняющийся сценарий, после чего приложение станет неработоспособным;
веб сервер может прервать длительное обращение к серверу 1С:Предприятия и вернуть ошибку 504 (шлюз не отвечает);
в случае длительного выполнения операции, у пользователя нет возможности отменить ее.

### Общий подход к асинхронному выполнению длительных серверных операций

Код, выполняющий длительную обработку данных, располагается в модуле менеджера объекта или в общем модуле. Результат своей работы он помещает во временное хранилище;

**Примечание**: необходимо использовать процедуру-обертку в общем модуле, которая будет вызывать процедуру модуля менеджера через ОбщегоНазначения.ВыполнитьМетодКонфигурации. Т.к. фоновые задания могут работать только с процедурами и функциями общих модулей.

Как происходит длительная операция:

1) Для выполнения асинхронного кода, запускается фоновое задание
2) В течение 0.8 сек ожидается завершение задания. Если фоновое задание не завершилось через 0.8 сек, управление передается клиенту
3) В клиентском коде подключается обработчик ожидания, в котором периодически проверяется состояние фонового задания. При этом интервал опроса задания увеличивается от 1 до 15 секунд с фиксированным коэффициентом 1.4
4) На время выполнения длительной операции пользователю отображается индикатор. Для отчетов индикатор выводится в поле табличного документа, используя свойство поля табличного документа `ОтображениеСостояния`:

![](https://its.1c.ru/db/content/v8std/src/1%C2%A0200/i8100642.files/%D0%B4%D0%BE.png?_=00000A30EF06BC16-v2)


а для прочих мест – выводится блокирующая форма (РежимОткрытияОкна = БлокироватьОкноВладельца), на которой размещена декорация с анимированной картинкой и кнопка «Отмена»:

![](https://its.1c.ru/db/content/v8std/src/1%C2%A0200/i8100642.files/%D0%B4%D0%BB%D0%B8%D1%82%D0%B5%D0%BB%D1%8C%D0%BD%D0%B0%D1%8F%D0%BE%D0%BF%D0%B5%D1%80%D0%B0%D1%86%D0%B8%D1%8F.png?_=000012D96E66F4F0-v2)

При получении от сервера информации о том, что фоновое задание завершено, полученный результат загружается из временного хранилища и обрабатывается.

> Асинхронное формирование отчетов должно, преимущественно, выполняться для СКД-отчетов

При использовании в конфигурации Библиотеки стандартных подсистем в распоряжении разработчика имеются вспомогательные функции и процедуры общих модулей `ДлительныеОперации`, `ДлительныеОперацииКлиент`, а также процедура `УстановитьСостояниеПоляТабличногоДокумента` общего модуля `ОбщегоНазначенияКлиентСервер`

Пример выполнения функции в фоновом задании при использовании в конфигурации Библиотеки стандартных подсистем. В модуле менеджера объекта размещена функция, которая выполняет поиск настроек и возвращает их:

```bsl
Функция ОпределитьНастройкиУчетнойЗаписи(АдресЭлектроннойПочты, Пароль) Экспорт
  ...
  Возврат Настройки;
КонецФункции
```

В форме объекта выполняется вызов этой функции в фоновом задании в три этапа:
1) запуск фонового задания на сервере,
2) подключение обработчика завершения фонового задания на клиенте,
3) обработка результата выполнения фонового задания.

```bsl
&НаКлиенте
Процедура НастроитьПараметрыПодключенияАвтоматически()

    // 1. Запуск фонового задания на сервере.
    ДлительнаяОперация = НачатьПоискНастроекУчетнойЗаписи();
    
    // 2. Подключение обработчика завершения фонового задания.
    ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
    Оповещение = Новый ОписаниеОповещения("ПриЗавершенииПоискаНастроек", ЭтотОбъект);
    ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция НачатьПоискНастроекУчетнойЗаписи()

    ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
    Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
    "Справочники.УчетныеЗаписиЭлектроннойПочты.ОпределитьНастройкиУчетнойЗаписи",
    АдресЭлектроннойПочты, Пароль);

КонецФункции

// Обработка результата выполнения фонового задания.

// Параметры:
// Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
// ДополнительныеПараметры - Неопределено
&НаКлиенте
Процедура ПриЗавершенииПоискаНастроек(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат = Неопределено Тогда // Пользователь отменил задание.
        Возврат;
    КонецЕсли;
    
    Если Результат.Статус = "Ошибка" Тогда
    СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
        Возврат;
    КонецЕсли;
    
    Настройки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
    УдалитьИзВременногоХранилища(Результат.АдресРезультата);
    УстановитьНастройкиУчетнойЗаписи(Настройки);

КонецПроцедуры
```

### Оптимизация

При каждом выполнении фонового задания его результат помещается во временное хранилище на время жизни формы:

```bsl
ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ПараметрФоновогоЗадания);
```

Если длительная операция выполняется пользователем многократно, пока эта форма открыта, то временные хранилища накапливаются, что вызывает рост потребления памяти. Поэтому для уменьшения расхода оперативной памяти в большинстве случаев рекомендуется очищать временное хранилище сразу после получения результата фонового задания:

```bsl
Настройки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
УдалитьИзВременногоХранилища(Результат.АдресРезультата); // Данные во временном хранилище больше не требуются.
```

Если же результат фонового задания требуется сохранять на протяжении нескольких серверных вызовов, то необходимо передавать фиксированный адрес заранее инициализированного временного хранилища:

```bsl
&НаСервере
Процедура ПриСозданииНаСервере(Отказ)

    // Резервируем адрес временного хранилища
    АдресРезультатаФоновогоЗадания = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор); 

КонецПроцедуры

&НаСервере
Функция НачатьПоискНастроекУчетнойЗаписи()

    ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
    ПараметрыВыполнения.АдресРезультата = АдресРезультатаФоновогоЗадания; // всегда используем одно и то же временное хранилище
    
    Возврат ДлительныеОперации.ВыполнитьФункцию(
        ПараметрыВыполнения,
        "Справочники.УчетныеЗаписиЭлектроннойПочты.ОпределитьНастройкиУчетнойЗаписи",
        АдресЭлектроннойПочты,
        Пароль
    );

КонецФункции
```

### Запуск фоновых заданий из кода

Если в конфигурации реализуются алгоритмы, инициирующие запуск фоновых заданий или запись данных информационной базы без участия пользователя (например, регулярное обновление информации в открытой форме), то в них следует проверять, что в текущем сеансе не установлен монопольный режим. В противном случае, следует блокировать попытки выполнения таких действий. Например:

```bsl
Если МонопольныйРежим() тогда возврат; КонецЕсли;

ФоновыеЗадания.Выполнить("...
```

В некоторых случаях возникает необходимость в выполнении длительных операций, требующих установки монопольного режима доступа к информационной базе. Например:

- Обновление данных ИБ при первом интерактивном запуске программы после обновления конфигурации;
- Удаление объектов, помеченных на удаление;
- Выгрузка данных информационной базы в файл для перехода в сервис;
- Использования монопольного режима для снижения времени выполнения массовых операций по изменению данных;

При этом необходимо сначала устанавливать монопольный режим, а затем выполнять запуск фонового задания, которое реализует саму длительную операцию. В этом случае фоновым заданием будет унаследован монопольный режим, ранее установленный из пользовательского сеанса

На время выполнения этого фонового задания следует блокировать весь интерфейс приложения, открывая форму ожидания завершения операции в режиме `РежимОткрытияОкна = БлокироватьВесьИнтерфейс`. Блокировать интерфейс приложения требуется потому, что на время выполнения задания полноценная работа пользователя с приложением уже невозможна:

- Если пользователь или программа попытается записать какой-либо объект, это приведет к ошибке (из-за установленного монопольного режима);
- В ряде случаев могут запускаться фоновые задания в качестве реакции на действия пользователя случае (при поиске в динамическом списке, при вводе по строке, формировании отчетов и пр.), которые также завершатся с ошибкой.
- Кроме того, на самой форме ожидания длительной операции не следует размещать элементы управления, которые могут приводить к запуску таких фоновых заданий. Например: поля ввода, динамические списки и отчеты.

----

Механизм заданий предназначен для выполнения какой-либо прикладной функциональности по расписанию или асинхронно

**Механизм заданий решает следующие задачи:**

- возможность определения регламентных процедур на этапе конфигурирования системы;

- выполнение заданных действий по расписанию;

- выполнение вызова заданной процедуры или функции асинхронно, т. е. без ожидания ее завершения;

- отслеживание хода выполнения определенного задания и получение его статуса завершения (значения, указывающего успешность или неуспешность его выполнения);

- получение списка текущих заданий;

- возможность ожидания завершения одного или нескольких заданий;

- управление заданиями (возможность отмены, блокировка выполнения и др.).

**Механизм заданий состоит из следующих компонентов:**

- метаданных регламентных заданий,

- регламентных заданий,

- фоновых заданий,

- планировщика заданий.

**Регламентные задания** предназначены для выполнения прикладных задач по расписанию. Регламентные задания хранятся в информационной базе и создаются на основе метаданных, определяемых в конфигурации. Метаданные регламентного задания содержат такую информацию, как наименование, метод, использование и т. д.

Регламентное задание имеет расписание, которое определяет, в какие моменты времени нужно выполнять связанный с регламентным заданием метод. Расписание, как правило, задается в информационной базе, но может быть задано и на этапе конфигурирования (например, для предопределенных регламентных заданий).

## Фоновые задания

Фоновые задания предназначены для выполнения прикладных задач асинхронно, реализуются средствами встроенного языка.

Планировщик заданий используется для планирования выполнения регламентных заданий. Для каждого регламентного задания планировщик периодически проверяет, соответствует ли текущая дата и время расписанию регламентного задания. Если соответствует, планировщик назначает такое задание на выполнение. Для этого по данному регламентному заданию планировщик создает фоновое задание, которое и выполняет реальную обработку.

Фоновые задания удобно использовать для выполнения сложных вычислений, когда результат вычисления может быть получен через продолжительное время. Механизм заданий имеет средства для выполнения таких вычислений асинхронно.

С фоновым заданием связан метод, который вызывается при выполнении фонового задания.

- В качестве метода фонового задания может выступать любая процедура или функция не глобального общего модуля, которую можно вызвать на сервере

- Параметрами фонового задания могут быть любые значения, которые разрешено передавать на сервер

- Параметры фонового задания должны в точности соответствовать параметрам той процедуры или функции, которую оно вызывает

- Параметры фонового задания, в сериализованном виде, не могут превышать размер в 1 Гбайт. При превышении размера, генерируется исключение

- Если методом фонового задания является функция, то ее возвращаемое значение игнорируется

> Фоновое задание может иметь ключ ‑ значение любого типа данных. Ключ вводит ограничение на запуск фоновых заданий ‑ в единицу времени может выполняться только одно фоновое задание с определенным значением ключа и заданным именем метода фонового задания (имя метода состоит из имени модуля и имени процедуры или функции)

Ключ позволяет группировать фоновые задания, имеющие одинаковые методы, по определенному прикладному признаку с тем, чтобы в рамках одной группы выполнялось не более одного фонового задания

- Создание и управление фоновыми заданиями выполняются программно из любого соединения

- Создавать фоновое задание разрешено любому пользователю. При этом оно выполняется от имени того пользователя, который его создал

- Получать задания, а также ожидать их завершения разрешено из любого соединения пользователю с административными правами либо пользователю, который создал эти фоновые задания

- Фоновое задание является чисто сеансовым объектом, но не принадлежит какому-либо пользовательскому сеансу

- Для каждого задания создается специальный системный сеанс, выполняющийся от имени того пользователя, который выполнил вызов

- Фоновые задания не имеют сохраняемого состояния

Фоновое задание может порождать другие фоновые задания. В клиент-серверном варианте это позволяет распараллеливать сложные вычисления по рабочим процессам кластера, что может значительно ускорить процесс вычисления в целом. Распараллеливание реализуется порождением нескольких дочерних фоновых заданий с ожиданием завершения каждого из них в основном фоновом задании

- Фоновое задание имеет возможность помещать данные во временное хранилище вызывающего сеанса

- Передача данных из вызывающего сеанса в сеанс фонового задания невозможна

https://its.1c.ru/db/v8313doc#bookmark:dev:TI000002036

----

> **Фоновые задания** — это объекты конфигурации, которые являются механизмом параллельного выполнения работы

> Работа с фоновыми заданиями доступна только на сервере

При выполнении длительных действий в фоновом задании нет возможности получить “уведомление” от сервера, что фоновое задание завершено, чтобы получить подготовленные данные и загрузить их в табличную часть документа. Поэтому приходится подключать обработчик ожидания, который с заданной периодичностью проверяет состояние фонового задания с определенным идентификатором. Как только такая проверка определяет, что фоновое задание завершилось, можно приступать к обработке данных, помещенных во временное хранилище в фоновом задании.

> Длительные операции с использованием фоновых заданий и асинхронные методы – это принципиально разные механизмы асинхронного выполнения

---

Чтобы из клиентского приложения можно было проверить состояние запущенного фонового задания (завершено успешно, еще выполняется или завершено с ошибкой), его идентификатор также хранится в реквизите формы `ИдентификаторЗадания`

----

> Фоновое задание — это объект встроенного языка. Он является частью механизма заданий.

Фоновые задания предназначены для выполнения прикладных задач асинхронно. Они могут порождать дочерние фоновые задания, например, для распараллеливания сложных вычислений по различным рабочим серверам кластера в клиент-серверном варианте работы.

Существует возможность ограничить выполнение фоновых заданий, имеющих одинаковые методы, по определенному прикладному признаку. Программное создание и управление фоновыми заданиями возможно из любого соединения пользователя с информационной базой системы 1С:Предприятие 8. Фоновое задание выполняется от имени пользователя, который его создал.

Ведется история выполнения фоновых заданий. Все задания разделены на три типа (программные, системные, регламентные) и для каждого типа существует собственное ограничение на количество хранимых записей в истории. Это упрощает анализ истории в тех случаях, когда в системе исполняется большое количество заданий одного типа. Они вытесняют из истории только задания своего типа, а задания других типов остаются доступными для анализа.

Код из 1С:ERP:

```bsl
// Запустить выполнение процедуры в фоновом задании, если это возможно.
// При выполнении любого из следующих условий запуск выполняется не в фоне, а сразу в основном потоке:
//  * если вызов выполняется в файловой базе во внешнем соединении (в этом режиме фоновые задания не поддерживаются);
//  * если приложение запущено в режиме отладки (параметр /C РежимОтладки) - для упрощения отладки конфигурации;
//  * если в файловой ИБ имеются активные фоновые задания - для снижения времени ожидания пользователя;
//  * если выполняется процедура модуля внешней обработки или внешнего отчета.
//
// Не следует использовать эту функцию, если необходимо безусловно запускать фоновое задание.
// Может применяться совместно с функцией ДлительныеОперацииКлиент.ОжидатьЗавершение.
// 
// Параметры:
//  ИмяПроцедуры           - Строка    - имя экспортной процедуры общего модуля, модуля менеджера объекта 
//                                       или модуля обработки, которую необходимо выполнить в фоне.
//                                       Например, "МойОбщийМодуль.МояПроцедура", "Отчеты.ЗагруженныеДанные.Сформировать"
//                                       или "Обработки.ЗагрузкаДанных.МодульОбъекта.Загрузить". 
//                                       У процедуры должно быть два или три формальных параметра:
//                                        * Параметры       - Структура - произвольные параметры ПараметрыПроцедуры;
//                                        * АдресРезультата - Строка    - адрес временного хранилища, в которое нужно
//                                          поместить результат работы процедуры. Обязательно;
//                                        * АдресДополнительногоРезультата - Строка - если в ПараметрыВыполнения установлен 
//                                          параметр ДополнительныйРезультат, то содержит адрес дополнительного временного
//                                          хранилища, в которое нужно поместить результат работы процедуры. Опционально.
//                                       При необходимости выполнить в фоне функцию ее следует обернуть в процедуру,
//                                       а ее результат возвращать через второй параметр АдресРезультата.
//  ПараметрыПроцедуры     - Структура - произвольные параметры вызова процедуры ИмяПроцедуры.
//  ПараметрыВыполнения    - Структура - см. функцию ДлительныеОперации.ПараметрыВыполненияВФоне.
//
// Возвращаемое значение:
//  Структура              - параметры выполнения задания: 
//   * Статус               - Строка - "Выполняется", если задание еще не завершилось;
//                                     "Выполнено", если задание было успешно выполнено;
//                                     "Ошибка", если задание завершено с ошибкой;
//                                     "Отменено", если задание отменено пользователем или администратором.
//   * ИдентификаторЗадания - УникальныйИдентификатор - если Статус = "Выполняется", то содержит 
//                                     идентификатор запущенного фонового задания.
//   * АдресРезультата       - Строка - адрес временного хранилища, в которое будет
//                                     помещен (или уже помещен) результат работы процедуры.
//   * АдресДополнительногоРезультата - Строка - если установлен параметр ДополнительныйРезультат, 
//                                     содержит адрес дополнительного временного хранилища,
//                                     в которое будет помещен (или уже помещен) результат работы процедуры.
//   * КраткоеПредставлениеОшибки   - Строка - краткая информация об исключении, если Статус = "Ошибка".
//   * ПодробноеПредставлениеОшибки - Строка - подробная информация об исключении, если Статус = "Ошибка".
// 
// Пример:
//  В общем виде процесс запуска и обработки результата длительной операции выглядит следующим образом:
//
//   1) Процедура, которая будет исполняться в фоне, располагается в модуле менеджера объекта или в серверном общем модуле:
//    Процедура ВыполнитьДействие(Параметры, АдресРезультата) Экспорт
//     ...
//     ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
//    КонецПроцедуры
//
//   2) Запуск операции на сервере и подключение обработчика ожидания:
//    &НаКлиенте
//    Процедура ВыполнитьДействие()
//     ДлительнаяОперация = НачатьВыполнениеНаСервере();
//     ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
//     ...
//     ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьДействиеЗавершение", ЭтотОбъект);
//     ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
//    КонецПроцедуры
//
//    &НаСервере
//    Функция НачатьВыполнениеНаСервере()
//     ПараметрыПроцедуры = Новый Структура;
//     ...
//     ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
//     ...
//     Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.МояОбработка.ВыполнитьДействие", 
//     ПараметрыПроцедуры, ПараметрыВыполнения);
//    КонецФункции
//    
//   3) Обработка результата выполнения операции:
//    &НаКлиенте
//    Процедура ВыполнитьДействиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
//     Если Результат = Неопределено Тогда
//      Возврат;
//     КонецЕсли;
//     ВывестиРезультат(Результат);
//    КонецПроцедуры 
//  
Функция ВыполнитьВФоне(Знач ИмяПроцедуры, Знач ПараметрыПроцедуры, Знач ПараметрыВыполнения) Экспорт
    
    ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ДлительныеОперации.ВыполнитьВФоне", "ПараметрыВыполнения", 
        ПараметрыВыполнения, Тип("Структура")); 
    Если ПараметрыВыполнения.ЗапуститьНеВФоне И ПараметрыВыполнения.ЗапуститьВФоне Тогда
        ВызватьИсключение НСтр("ru = 'Параметры ""ВсегдаНеВФоне"" и ""ВсегдаВФоне""
            |не могут одновременно принимать значение Истина в ДлительныеОперации.ВыполнитьВФоне.';
            |en = 'The ""ВсегдаНеВФоне"" and ""ВсегдаВФоне""
            |parameters cannot be both True in ДлительныеОперации.ВыполнитьВФоне.'");
    КонецЕсли;
#Если ВнешнееСоединение Тогда
    ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
    Если ПараметрыВыполнения.БезРасширений И ИнформационнаяБазаФайловая Тогда
        ВызватьИсключение НСтр("ru = 'Фоновое задание не может быть запущено с параметром ""БезРасширений""
            |в файловой информационной базе в ДлительныеОперации.ВыполнитьВФоне.';
            |en = 'Background job cannot be started with the ""БезРасширений"" parameter
            |in the file infobase in ДлительныеОперации.ВыполнитьВФоне.'");
    КонецЕсли;
#КонецЕсли
     
    АдресРезультата = ?(ПараметрыВыполнения.АдресРезультата <> Неопределено, 
        ПараметрыВыполнения.АдресРезультата,
        ПоместитьВоВременноеХранилище(Неопределено, ПараметрыВыполнения.ИдентификаторФормы));
    
    Результат = Новый Структура;
    Результат.Вставить("Статус",    "Выполняется");
    Результат.Вставить("ИдентификаторЗадания", Неопределено);
    Результат.Вставить("АдресРезультата", АдресРезультата);
    Результат.Вставить("АдресДополнительногоРезультата", "");
    Результат.Вставить("КраткоеПредставлениеОшибки", "");
    Результат.Вставить("ПодробноеПредставлениеОшибки", "");
    Результат.Вставить("Сообщения", Новый ФиксированныйМассив(Новый Массив));
    
    Если ПараметрыВыполнения.БезРасширений Тогда
        ПараметрыВыполнения.БезРасширений = ЗначениеЗаполнено(ПараметрыСеанса.ПодключенныеРасширения);
    КонецЕсли;
    
    ПараметрыЭкспортнойПроцедуры = Новый Массив;
    ПараметрыЭкспортнойПроцедуры.Добавить(ПараметрыПроцедуры);
    ПараметрыЭкспортнойПроцедуры.Добавить(АдресРезультата);
    
    Если ПараметрыВыполнения.ДополнительныйРезультат Тогда
        Результат.АдресДополнительногоРезультата = ПоместитьВоВременноеХранилище(Неопределено, ПараметрыВыполнения.ИдентификаторФормы);
        ПараметрыЭкспортнойПроцедуры.Добавить(Результат.АдресДополнительногоРезультата);
    КонецЕсли;
    
#Если ВнешнееСоединение Тогда
    ВыполнитьБезФоновогоЗадания = ИнформационнаяБазаФайловая 
        Или ОбщегоНазначения.РежимОтладки() Или ПараметрыВыполнения.ЗапуститьНеВФоне
        Или (ЕстьФоновыеЗаданияВФайловойИБ() И Не ПараметрыВыполнения.ЗапуститьВФоне) 
        Или Не ВозможноВыполнитьВФоне(ИмяПроцедуры);
#Иначе
    ВыполнитьБезФоновогоЗадания = Не ПараметрыВыполнения.БезРасширений
        И (ОбщегоНазначения.РежимОтладки() Или ПараметрыВыполнения.ЗапуститьНеВФоне
            Или (ЕстьФоновыеЗаданияВФайловойИБ() И Не ПараметрыВыполнения.ЗапуститьВФоне) 
            Или Не ВозможноВыполнитьВФоне(ИмяПроцедуры));
#КонецЕсли

    // Выполнить в основном потоке.
    Если ВыполнитьБезФоновогоЗадания Тогда
        Попытка
            ВыполнитьПроцедуру(ИмяПроцедуры, ПараметрыЭкспортнойПроцедуры);
            Результат.Статус = "Выполнено";
        Исключение
            Результат.Статус = "Ошибка";
            Результат.КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
            Результат.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
            ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка выполнения';
                                            |en = 'Execution error'", ОбщегоНазначения.КодОсновногоЯзыка()),
                УровеньЖурналаРегистрации.Ошибка, , , Результат.ПодробноеПредставлениеОшибки);
        КонецПопытки;
        Возврат Результат;
    КонецЕсли;
    
    // Выполнить в фоне.
    БезопасныйРежим = БезопасныйРежим();
    УстановитьОтключениеБезопасногоРежима(Истина);
    Попытка
        Задание = ЗапуститьФоновоеЗаданиеСКонтекстомКлиента(ИмяПроцедуры,
            ПараметрыВыполнения, ПараметрыЭкспортнойПроцедуры, БезопасныйРежим);
    Исключение
        Результат.Статус = "Ошибка";
        Если Задание <> Неопределено И Задание.ИнформацияОбОшибке <> Неопределено Тогда
            Результат.КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
            Результат.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
        Иначе
            Результат.КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
            Результат.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        КонецЕсли;
        Возврат Результат;
    КонецПопытки;
    УстановитьОтключениеБезопасногоРежима(Ложь);
    
    Если Задание <> Неопределено И Задание.ИнформацияОбОшибке <> Неопределено Тогда
        Результат.Статус = "Ошибка";
        Результат.КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
        Результат.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
        Возврат Результат;
    КонецЕсли;
    
    Результат.ИдентификаторЗадания = Задание.УникальныйИдентификатор;
    ЗаданиеВыполнено = Ложь;
    
    Если ПараметрыВыполнения.ОжидатьЗавершение <> 0 Тогда
        Попытка
            Задание.ОжидатьЗавершения(ПараметрыВыполнения.ОжидатьЗавершение);
            ЗаданиеВыполнено = Истина;
        Исключение
            // Специальная обработка не требуется, возможно исключение вызвано истечением времени ожидания.
        КонецПопытки;
    КонецЕсли;
    
    Если ЗаданиеВыполнено Тогда
        ПрогрессИСообщения = ПрочитатьПрогрессИСообщения(Задание.УникальныйИдентификатор, "ПрогрессИСообщения");
        Результат.Сообщения = ПрогрессИСообщения.Сообщения;
    КонецЕсли;
    
    ЗаполнитьЗначенияСвойств(Результат, ОперацияВыполнена(Задание.УникальныйИдентификатор), , "Сообщения");
    Возврат Результат;
    
КонецФункции
```

#1С #1С/Многопоточность
