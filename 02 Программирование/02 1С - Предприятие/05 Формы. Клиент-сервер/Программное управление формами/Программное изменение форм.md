Форма не рождается на клиенте. Форма рождается на сервере, она проходит несколько важных стадий предварительной обработки, прежде чем достичь глаз пользователя. Платформа содержит достаточно сложные механизмы многоуровневого кеширования различных частей формы как на сервере, так и на клиенте.

Формы спроектированы так, чтобы их было легко конструировать в конфигураторе. Хоть для форм определен достаточный функционал по программному изменению элементов, программное изменение форм рекомендуется использовать лишь в отдельных специфических сценариях работы.

Нельзя программно удалить элементы, реквизиты или команды, созданные в конфигураторе

Добавление и удаление реквизитов, команд и элементов формы возможно только на сервере

Программное изменение форм из других объектов конфигурации невозможно, изменения доступны только в модуле самой формы. Потому что изменение формы возможно только на сервере, а открытие формы – только на клиенте. А встроенный язык не содержит методов, позволяющих разработчику принудительно передать форму с сервера на клиент

Элементы и команды можно добавлять/удалять поодиночке, просто обращаясь к коллекциям этих элементов, используя методы `Добавить()` и `Удалить()`. Это относительно «безболезненные» операции для формы.

Изменение состава реквизитов, напротив, является сложной и затратной операцией. Поэтому здесь используется следующий подход. Сначала разработчик создает два массива программных объектов, которые описывают реквизиты формы. Один массив – это те реквизиты, которые должны быть добавлены, а другой массив – это те реквизиты, которые нужно удалить. После этого «за один подход» выполняется модификация формы с помощью ее метода ИзменитьРеквизиты(), в который передаются оба этих массива. Сначала выполняется удаление реквизитов, затем – добавление

> Программно можно создавать реквизиты формы, но нельзя делать их основными, поэтому полноценное программное создание формы невозможно

## Программное создание поля

Представим ситуацию: в пустой форме, в конфигураторе мы создаем реквизит формы `РеквизитКомментарий` с типом **`Строка`**. Затем мы размещаем реквизит на форме и добавляем обработчик события **`ПриИзменении`**:

```bsl
&НаКлиенте
Процедура ПриИзмененииРеквизитКомментарий(Элемент)
	ОбщегоНазначенияКлиент.Сообщить(РеквизитКомментарий);
КонецПроцедуры
```

Описанные действия, совершаемые в конфигураторе, можно сделать в коде. Добавим команду `ДобавитьПоле()` и зададим для нее обработчик

```bsl
&НаКлиенте
Процедура ДобавитьПоле(Команда)
	ДобавитьПолеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеНаСервере()

	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("Строка"));
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита);
	
	НовыйРеквизит = Новый РеквизитФормы(
		"РеквизитКомментарий",     // имя
		ОписаниеТиповДляРеквизита, // тип
		,                          // путь
		"Комментарий",             // заголовок
		Ложь                       // сохраняемые данные
	); 
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// Теперь на форме появился новый реквизит
	
	новыйЭлемент = Элементы.Добавить("ПолеКомментарий", Тип("ПолеФормы"));
	новыйЭлемент.ПутьКДанным = "РеквизитКомментарий";
	новыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	овыйЭлемент.КнопкаОчистки = Истина;
	
	// Теперь на форме появилось поле, связанное с созданным реквизитом
	
	новыйЭлемент.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитКомментарий");
	
	// Теперь для элемента формы определен обработчик события ПриИзменении
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРеквизитКомментарий(Элемент)
	ОбщегоНазначенияКлиент.Сообщить(РеквизитКомментарий);
КонецПроцедуры
```

Параметр конструктора `РеквизитФормы()` `СохраняемыеДанные` определяет, содержит ли реквизит сохраняемые данные. Если содержит, то при изменении этого реквизита будет автоматически устанавливаться модифицированность формы.

## Программное создание динамического списка

```bsl
&НаКлиенте
Процедура СоздатьДинамическийСписок(Команда)
	ДобавитьПолеНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьДинамическийСписокНаСервере()

	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("ДинамическийСписок"));
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита);
	
	НовыйРеквизит = Новый РеквизитФормы(
		"РеквизитСписокТоваров",     // имя
		ОписаниеТиповДляРеквизита,   // тип
		,                            // путь
		"СписокТоваров",             // заголовок
		Истина                       // сохраняемые данные
	);
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// Установка текста запроса и основной таблицы
	
	РеквизитСписок = РеквизитСписокТоваров;
	РеквизитСписок.ТекстЗапроса = "ВЫБРАТЬ Наименование, Код ИЗ Справочник.Товары";
	РеквизитСписок.ОсновнаяТаблица = "Справочник.Товары";
	
	// Добавление таблицы на форму
	
	НовыйЭлемент = Элементы.Добавить("ЭлементСписокТоваров", Тип("ТаблицаФормы"));
	НовыйЭлемент.ПутьКДанным = "РеквизитСписокТоваров";
	
	// Создание и добавление колонок на форму
	
	НоваяКолонкаТаблицы = Элементы.Добавить(
		"КолонкаНаименование",
		Тип("ПолеФормы"),
		НовыйЭлемент          // Родитель
	);
	НоваяКолонкаТаблицы.ПутьКДанным = "РеквизитСписокТоваров.Наименование";
	
	НоваяКолонкаТаблицы = Элементы.Добавить(
		"КолонкаКод",
		Тип("ПолеФормы"),
		НовыйЭлемент
	);
	НоваяКолонкаТаблицы.ПутьКДанным = "РеквизитСписокТоваров.Код";

КонецПроцедуры
```

В коде был приведен пример создания колонок для новой таблицы, созданной программно. Если требуется добавить новые колонки в уже существующую таблицу формы, нужно создавать полноценные реквизиты программно

```bsl
//Создание реквизита колонки

ТипыРеквизита = Новый Массив;
ТипыРеквизита.Добавить(Тип("Строка"));
ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита);

НовыйРеквизит = Новый РеквизитФормы(
	"РеквизитКолонкаЗанятость",  // имя
	ОписаниеТиповДляРеквизита,   // тип
	"РеквизитТаблицаЗначений",   // путь
	"Занятость",                 // заголовок
	Истина                       // сохраняемые данные
);

ДобавляемыеРеквизиты = Новый Массив;
ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
ИзменитьРеквизиты(ДобавляемыеРеквизиты);

// Добавление на форму

НовыйЭлемент = Элементы.Добавить(
	"КолонкаЗанятость",
	Тип("ПолеФормы"),
	Элементы.ЭлементТаблицаЗначений
);
НовыйЭлемент.ПутьКДанным = "РеквизитТаблицаЗначений.РеквизитКолонкаЗанятость";
НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
НовыйЭлемент.КнопкаОчистки = Истина;

// Обработчик события

НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПриИзмененииКолонкиЗанятость");
```

## Программное добавление команды

```bsl
&НаСервере
Процедура ДобавитьКомандуНаСервере()
	НоваяКоманда = Команды.Добавить("НоваяКоманда");
	НоваяКоманда.Действие = "КомандаФормыДобавленнаяПрограммно";
	
	// Создание кнопки и связывание с командой
	
	НовыйЭлемент = Элементы.Добавить(
		"КнопкаНоваяКоманда",
		Тип("КнопкаФормы"),
		Элементы.ФормаКоманднаяПанель
	);
	
	// Связь кнопки с командой
	
	НовыйЭлемент.ИмяКоманды = "НоваяКоманда";
	НовыйЭлемент.КнопкаПоУмолчанию = Истина;
КонецПроцедуры
```

#1С #1С/Формы #MERGE_NOTES/Shit