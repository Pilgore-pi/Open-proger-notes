## Набор записей

Тип `РегистрНакопленияЗапись.<Имя регистра накопления>`. Поддерживает проход в цикле

## Примеры кода

Запись в регистр накопления 

```bsl
НаборЗаписей = РегистрыНакопления.ОбеспечениеДенежныхСредств.СоздатьНаборЗаписей();

// Если нужно фильтровать по регистратору
НаборЗаписей.Отбор.Регистратор.Значение = Объект.Ссылка;

НоваяЗапись = НаборЗаписей.Добавить();
НоваяЗапись.ВидДвижения = ВидДвиженияНакопления.Расход;
НоваяЗапись.Период = Объект.Дата;

// Пользовательские реквизиты
НоваяЗапись.Заказ = Объект.Заказ;
НоваяЗапись.Организация = Объект.Организация;
НоваяЗапись.Контрагент =Объект.Контрагент;
НоваяЗапись.Сумма =Объект.СуммаДокумента;

//Если нужно очищать то Не добавлять новую запись а просто записать после отбора
НаборЗаписей.Записать();
```

Удаление записей регистра

```bsl
НаборЗаписей = РегистрыНакопления.Движ.СоздатьНаборЗаписей();
НаборЗаписей.Отбор.Регистратор.Значение = Объект.Ссылка;
НаборЗаписей.Записать();
```

## Чтение

Чтение записей регистра накопления

```bsl
// Получим все записи регистра накопления ЗаказыКлиентов
// по организации ООО "Ромашка" за 1 квартал 2014 года,
// упорядочив их по возрастанию даты.

Запрос = Новый Запрос;
Запрос.Текст = 
    "ВЫБРАТЬ
    |   ЗаказыКлиентов.Период,
    |   ЗаказыКлиентов.Регистратор,
    |   ЗаказыКлиентов.НомерСтроки,
    |   ЗаказыКлиентов.Активность,
    |   ЗаказыКлиентов.Контрагент,
    |   ЗаказыКлиентов.Номенклатура,
    |   ЗаказыКлиентов.Количество
    |ИЗ
    |   РегистрНакопления.ЗаказыКлиентов КАК ЗаказыКлиентов
    |ГДЕ
    |   ЗаказыКлиентов.Активность = Истина И
    |   ЗаказыКлиентов.Организация = &ВыбОрганизация И
    |   ЗаказыКлиентов.Период МЕЖДУ &НачДата И &КонДата
    |УПОРЯДОЧИТЬ ПО
    |   ЗаказыКлиентов.Период";

Запрос.УстановитьПараметр("ВыбОрганизация",
    Справочники.Организации.НайтиПоНаименованию("ООО ""Ромашка""")
);
// начальная дата - 1 января 2014 года
Запрос.УстановитьПараметр("НачДата", '20140101000000');
// конечная дата - 31 марта 2014 года 23 часа 59 минут 59 секунд
Запрос.УстановитьПараметр("КонДата", '20140331235959');

РезультатЗапроса = Запрос.Выполнить();

Записи = РезультатЗапроса.Выбрать();

Пока Записи.Следующий() Цикл
    Сообщить(
        "[" + Записи.Период + "] " +
        Записи.Номенклатура + " " +
        Записи.Количество + " шт." +
        " (" + Записи.Регистратор + ")"
    );
КонецЦикла;
```

Чтение оборотов

```bsl
ВЫБРАТЬ
    ЗаказыКлиентовОбороты.Номенклатура,
    ЗаказыКлиентовОбороты.Организация,
    ЗаказыКлиентовОбороты.КоличествоОборот
ИЗ
    РегистрНакопления.ЗаказыКлиентов.Обороты (
        ДАТАВРЕМЯ(2014, 01, 01, 00, 00, 00),
        ДАТАВРЕМЯ(2014, 03, 31, 23, 59, 59),
        Период,
        Организация = &ВыбОрганизация
    ) КАК ЗаказыКлиентовОбороты
УПОРЯДОЧИТЬ ПО
    ЗаказыКлиентовОбороты.Номенклатура
```

Чтение остатков

```bsl
ВЫБРАТЬ
    ЗапасыЕдыОстатки.Склад,
    ЗапасыЕдыОстатки.Номенклатура,
    ЗапасыЕдыОстатки.Организация,
    ЗапасыЕдыОстатки.КоличествоОстаток
ИЗ
    РегистрНакопления.ЗапасыЕды.Остатки(
        &ВыбДата,
        Склад = &ВыбСклад И
        Номенклатура = &ВыбНоменклатура
    ) КАК ЗапасыЕдыОстатки
```

Остатки и обороты

```bsl
ВЫБРАТЬ
    ЗапасыЕдыОстаткиИОбороты.Период,
    ЗапасыЕдыОстаткиИОбороты.Номенклатура,
    ЗапасыЕдыОстаткиИОбороты.Организация,
    ЗапасыЕдыОстаткиИОбороты.КоличествоНачальныйОстаток,
    ЗапасыЕдыОстаткиИОбороты.КоличествоОборот,
    ЗапасыЕдыОстаткиИОбороты.КоличествоПриход,
    ЗапасыЕдыОстаткиИОбороты.КоличествоРасход,
    ЗапасыЕдыОстаткиИОбороты.КоличествоКонечныйОстаток
ИЗ
    РегистрНакопления.ЗапасыЕды.ОстаткиИОбороты(
        ДАТАВРЕМЯ(2014, 01, 01, 00, 00, 00),
        ДАТАВРЕМЯ(2014, 12, 31, 23, 59, 59),
        Месяц, ДвиженияИГраницыПериода,
        Организация = &ВыбОрганизация И
        Номенклатура = &ВыбНоменклатура
    ) КАК ЗапасыЕдыОстаткиИОбороты
УПОРЯДОЧИТЬ ПО
    ЗапасыЕдыОстаткиИОбороты.Период
```
